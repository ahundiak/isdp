;;***************************************************************************
;; RIS DOS Run-time Package Installation - Intergraph Corporation
;;***************************************************************************

;;***************************************************************************
DefineVariables
;;***************************************************************************
;; Feb. 94, Instalit for RIS 5.x - Sanju
Text [ScriptVersion] := 1.0.0.0
Text [CompanyName] := Intergraph Corporation
Text [ProductName]
Directory [ProductDirectory]
Directory [ReadmeFileName]
Directory [ReadmeInstructions]
Text [ProductVersion] := !version!
Directory [DefaultInstallationDirectory] := C:\RIS
Logical [OfferInstallationDirectoryChoice] := YesCharacter
Number [MinimumFilesSetting] := 0
Number [MinimumBuffersSetting] := 0
;;Directory [ProgramToRun] := BlankString
Logical [AddInstallDirectoryToPath] := NoCharacter
Text [MinimumOSVersion] := 3.0
Number [MinimumSystemMemoryRequiredPresent] := 0
Logical [AllowCGA] := YesCharacter
Logical [AllowMDA] := YesCharacter
Logical [Allow808x] := NoCharacter
Logical [Allow80286] := NoCharacter
Text [NetWorkInstallation] := AllowNetwork
Text [SpecialPopupColors] := WhiteOnBrown
Logical [NeedToTerminate] := NoCharacter
Logical [Installed] := NoCharacter
Text [DisplayAdapterSummary]
Text [CPUTypeSummary]
Text [NetworkActive]
Text [MathCoprocessorStatus]
Text [InstallationDriveSummary]
Text [OSVersionSummary]
Text [SystemMemorySummary]
Logical [InsufficientSystem] := NoCharacter
Text [BottomLine]
EndDefineVariables

[ProductName] := RIS-DOS Runtime
[ProductDirectory] := RISRT

;;[ReadmeFileName] := [InstallationDirectory]\[ProductDirectory]\README.TXT
[ReadmeFileName] := [InstallationDirectory]\README.TXT
;;[ReadmeInstructions] := [BootDrive]:\README.TXT
[ReadmeInstructions] := [BootDrive]:\README.TXT

ClearScreen
Do SetupInstalit
Do PrepareSystemReport

;; Find out if to run in auto mode
If [CommandLine1] Contains /f:
	[ResponseFile] := SubStringOf [CommandLine1] from 4 to End
	TextBox @xy(Center,4)
		Processing file [ResponseFile], please wait ...
	EndTextBox
	Do AutoInstallation
	SetExitMessageto "Update the autoexec.bat file before using [ProductName]"
	SoLong
Endif

;; get previous directory setting
[Dir1] := EnvironmentStringFor [ProductDirectory]
if [Dir1] <> BlankString
	[Number1] := PositionOf [ProductDirectory] in [Dir1]
	[Number1] := [Number1] -2
;;      [DefaultInstallationDirectory] := SubStringOf [Dir1] from 4 to [Number1]
	[DefaultInstallationDirectory] := SubStringOf [Dir1] from beginning to [Number1]
;; popup [DefaultInstallationDirectory]
Endif

If Windows  ;; INSTALIT for Windows does not currently support ScriptView
Popup This script was not designed for INSTALIT for Windows.
else  ;; by default ScriptView is off
	LetButton <F1> Do ShowHelpInfo
	LetButton <F2> Do VisitOS
	LetButton <F3> Do ShowSystemSummary
	LetButton <F4> Do ShowReadmeFile
	LetButton <F9> Do ExitInstallation
	LetButton <CtrlX> Do ExitInstallation
EndIf
If [CommandLine1] = -ReplaceFiles
	SetReplacementInquiry Off
EndIf
If [CommandLine2] = -ReplaceFiles
	SetReplacementInquiry Off
EndIf
If [CommandLine3] = -ReplaceFiles
	SetReplacementInquiry Off
EndIf

;;***************************************************************************
:Again
;;***************************************************************************
ClearScreen
QueAllFiles
If QueSize = 0
	GoTo Continue
EndIf

If [InsufficientSystem] = YesCharacter
	Dialog [YesOrNo1] WhiteOnRed ;; Insufficient system notice
		Analysis of your system has determined that it is
		not compatible with [ProductName].  For a complete
		summary, press <F3>.

		Do you wish to continue anyway?
	EndDialog
	If [YesOrNo1] = NoCharacter
		GoTo Again
	EndIf
EndIf

If [Installed] = NoCharacter
;;      DetermineInstallationDrive QueSize RequireFixed

;;      [InstallationDirectory] := InstallationDrive:\[DefaultInstallationDirectory]
	If [OfferInstallationDirectoryChoice] = YesCharacter
		DetermineInstallationDirectory [DefaultInstallationDirectory] QueSize RequireFixed
	else
		EndIf
EndIf

SetBottomLineTo     BlankString
Logging on
SetLogFileTo [InstallationDirectory]\risrt.log

[Dir1] := EnvironmentStringFor [ProductDirectory]

GetQuedFiles ShowJustOne

SetBottomLineTo "<F1> Help <F2> Exit to DOS <F3> System Summary <F4> Readme File <F9> to Quit"

[Installed] := YesCharacter
If QueSize <> 0  ;; Some files were selected but not installed if <> 0
	[Installed] := NoCharacter
	UnqueAllFiles  ;; Remove uninstalled files from que
	Dialog ;; Message when unable to get all necessary files
		Unable to successfully complete this option.
	EndDialog
EndIf
:Continue
Do ExitInstallation


;;***************************************************************************
Procedure ShowHelpInfo
;;***************************************************************************
	Dialog PressAKey [SpecialPopupColors]  ;; How to reach technical support
		For assistance, call [CompanyName] Support.
		You are installing [ProductName] product.

		You can get a summary of your computer system
		by pressing <F3>.  This information will be
		important should you call Support.
	EndDialog
EndProcedure

;;***************************************************************************
Procedure ShowSystemSummary  ;; System summary - change with care.
;;***************************************************************************
	If [InstallationDirectory] <> BlankString
		[InstallationDriveSummary] := InstallationDrive (FreeSpaceOnDrive InstallationDrive bytes free)
	else
		[InstallationDriveSummary] := (Not Selected)
	EndIf
	Dialog PressAKey [SpecialPopupColors]
		.cHERE IS A SUMMARY OF YOUR SYSTEM
		.c[String8]  ;; notes whether any incompatibilities were found
		Product Name: [ProductName]
		Product Version: [ProductVersion]
		Installation Script Version: [ScriptVersion]

		Operating System Version:  [OSVersionSummary]
		CPU Type:  [CPUTypeSummary]
		Display Adapter:  [DisplayAdapterSummary]
		Math Coprocessor:  [MathCoprocessorStatus]
		Main Memory Present:  [SystemMemorySummary]
		Extended Memory Present:  ExtendedMemoryPresentK
		XMS Memory Available:  XMSMemoryAvailableK
		Installation Drive:  [InstallationDriveSummary]
		Network:  [NetworkActive]
		[String9]  ;; information about network suitability
	EndDialog
EndProcedure

;;***************************************************************************
Procedure PrepareSystemReport
;;***************************************************************************
	[InsufficientSystem] := NoCharacter
	If OSVersion < [MinimumOSVersion]
		[OSVersionSummary] := OSVersion (need at least [MinimumOSVersion])
		[InsufficientSystem] := YesCharacter
	else
		[OSVersionSummary] := OSVersion
	EndIf
	If MainMemory < [MinimumSystemMemoryRequiredPresent]
		[SystemMemorySummary] := MainMemoryK (need at least [MinimumSystemMemoryRequiredPresent]K)
		[InsufficientSystem] := YesCharacter
	else
		[SystemMemorySummary] := MainMemoryK
	EndIf
	If 808X
		If [Allow808X] = YesCharacter
			[CPUTypeSummary] := 808x
		else
			[CPUTypeSummary] := 808x (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If 80286
		[CPUTypeSummary] := 80286
	EndIf
	If 80386
		[CPUTypeSummary] := 80386
	EndIf
	If 80486
		[CPUTypeSummary] := 80486
	EndIf
	If [CPUTypeSummary] = BlankString
		[CPUTypeSummary] := 808x
	EndIf
	If MicroChannel
		[CPUTypeSummary] := [CPUTypeSummary] (in MicroChannel Architecture)
	else
		[CPUTypeSummary] := [CPUTypeSummary] (ISA or EISA Architecture)
	EndIf
	If CGA
		If [AllowCGA] = YesCharacter
			[DisplayAdapterSummary] := CGA
		else
			[DisplayAdapterSummary] := CGA (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If MDA
		If [AllowMDA] = YesCharacter
			[DisplayAdapterSummary] := MDA
		else
			[DisplayAdapterSummary] := MDA (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If EGA
		[DisplayAdapterSummary] := EGA
	EndIf
	If VGA
		[DisplayAdapterSummary] := VGA
	EndIf
	If HGC
		[DisplayAdapterSummary] := HGC
	EndIf
	If HGCPlus
		[DisplayAdapterSummary] := HGC Plus
	EndIf
	If Network
		[NetworkActive] := Network Active
	else
		[NetworkActive] := No Network Active
	EndIf
	If NovellLoaded
		[NetworkActive] := Novell [NetworkActive]
	EndIf
	If MathCoprocessorPresent
		[MathCoprocessorStatus] := Present
	else
			[MathCoprocessorStatus] := Not Present
	EndIf

	If [InsufficientSystem] = YesCharacter
		[String8] := (Please note the listed incompatibilities)
	EndIf
	If [NetworkInstallation] contains DisallowNetwork
		[String9] := "   (You cannot install this version on a network drive)"
	EndIf
EndProcedure

Procedure VisitOS
	SetAllowExit off
	Dialog [YesOrNo1] [SpecialPopupColors]  ;; Notice before exiting to OS
		Do you wish to exit to the operating system and
		then return?
	EndDialog
	If [YesOrNo1] = YesCharacter
		ExitToDOS
	EndIf
	SetAllowExit on
EndProcedure

;;***************************************************************************
Procedure ExitInstallation
;;***************************************************************************
	SetAllowExit Off

	ClearScreen

	;; Ask to read the product readme.txt file
	If [Installed] = YesCharacter
		Do ShowReadmeFile
	EndIf
	If [Installed] = YesCharacter
		Dialog ;; Final Farewell
			Installation for [ProductName] is complete!
		EndDialog
		SetExitMessageto "Update the autoexec.bat file before using [ProductName]"
	Endif
	SoLong
EndProcedure

;;***************************************************************************
Procedure SetupInstalit
;;***************************************************************************
	SetShadows On
  SetBackgroundCharTo B0
  SetMainBorderTo     None
  SetPopupBorderTo    Single
  SetModifyModeTo     Example
  SetShadowAttrTo     DarkGrayOnBlack
  SetScreenAttrTo     WhiteOnBlue
  SetPopupAttrTo      BlueOnWhite
  SetAttentionAttrTo  WhiteOnRed
  SetTopLineAttrTo    BlackOnCyan
  SetBottomLineAttrTo WhiteOnLightGray
;;  SetBottomLineTo "   <F1> Help    <F2> Exit to DOS   <F3> System Summary   <F9> to Quit    "
  SetBottomLineTo "<F1> Help <F2> Exit to DOS <F3> System Summary <F4> Readme File <F9> to Quit"

  SetTopLineTo        "  [CompanyName]'s [ProductName] Version [ProductVersion]  "

EndProcedure

;;***************************************************************************
Procedure ShowReadmeFile
;;***************************************************************************
;; Ask to read the product readme.txt file
If [Installed] = YesCharacter
	[ReadmeFileName] := [InstallationDirectory]\readme.txt
else
	[ReadmeFileName] := [InstallFromDirectory]\readme.txt
Endif
SetAllowExit Off
If [Installed] = YesCharacter
	Dialog [YesOrNo1]
		Do you want to read the [ProductName] readme file now?
	EndDialog
else
	[YesOrNo1] := YesCharacter
Endif
	If [YesOrNo1] = YesCharacter
		If FileExists [ReadmeFileName]
			BrowseFile [ReadmeFileName]
		else
			Popup Sorry, readme file [ReadMeFileName] is missing.
		EndIf
	EndIf
SetAllowExit On
EndProcedure

Procedure AutoInstallation
	ExamineTextFile [ResponseFile]
		[DefaultInstallationDirectory] := SettingFor DestPath
	ForgetTextFile

	[InstallationDirectory] := [DefaultInstallationDirectory]
	ClearScreen
	Logging On
	SetLogFileTo [InstallationDirectory]\risrt.log

	GetQuedFiles ShowJustOne
	[Installed] := YesCharacter
EndProcedure




