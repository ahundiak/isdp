#******************************************************************************
#	STRUDEL FILE FOR AVONDALE PROFILE CARDS
#
#	Maciej Jablkowski - INTERGRAPH Corp., March 9th, 2000
#
# Strudel file contains rules for extracting 3D model stiffeners 
# to the 2D profile card not-to-scale manufacturing sketches
#
# This file contains common code for penetrations (Cut Throughs)
#
#	This file uses external PPL program "PcGen.e" which helps to 
#	determine a name of a 2D symbol macro to place on the profile
#	card from striffener type being processed and aplied macros.
#
# History:	MJ	03-09-2000 - Creation
#		MJ	08-02-2001 - Major review and update.
#		MJ	09-18-2001 - PPL calls reduced by using attributes
#
#******************************************************************************

# Turn report mode on/off

silent;
#verbose;

	#******************************
	# Get stock value
	#******************************
	$stif 		:= vl-pc-GetStiff();
	$prof_len 	:= vl-GetAttributeVal( "ProfileLength" );
	$start_stock    := vl-GetAttributeVal( "StartStock" );

	#******************************
	# Get view & macro coord sys
	#******************************
	$csO 		:= vl-pc-ViewCs();
	$csA 		:= vl-pc-ViewMcCs("noangle");
	
	#********************
	# Get penetration id 
	#********************
	$label		:= attribute("id:id ");

	#***********************************************
	# Get penetrating profile & its family name
	#***********************************************
	$pg_stf		:= get-input("Intersect Beam");
        $pg_prof_type	:= attribute("family_name", $pg_stf);

	#*******************************************
	# Run PPL to check penetrating profile type
	#*******************************************
	$pg_prof_type 	:= exec-ppl-func($ppl, $ppl, $type, 
				$ProfCodeFile, $pg_prof_type, "graphicPc.cth" );

	#***************************************************
	# Get the position of the macro, left ET and length
	#***************************************************
	$macPos 	:= vl-pc-McPosition();
	$ETpos		:= vl-GetAttributeVal( "FirstETPos" ); 
	$macPosX	:= vl-pc-CvrtDouble( get-word( $macPos, 0 ));	

	#**************************************************************
	# Proceed only if penetration is placed between end treatments
	#**************************************************************
	select when ( $macPosX > $ETpos ) & ( $macPosX < $prof_len ) : 

		#*****************************
		# Place penetration 2d macro
		#*****************************
		vl-PlaceMacro( $pntr_mac, $csO, $csA, $spntr, $spntr, $pg_prof_type[0], 
				$mac_type, $label, $txt_ht)
			[color = 13];

		#***************************************
		# Put a X dimension on the profile card
		#***************************************
	        $xbase          := vl-pc-GetXBase( 0 );
		$formatX 	:= $macPosX-$FirstEtPos+$xbase+$start_stock;
		$numcharX 	:= string-length($formatX); 
		$format		:= format-number(
						catenate-strings($numcharX,
							".0") , $formatX );

		#******************************
		# Set stacked dimension offset
		#******************************
		$gap 		:= vl-GetAttributeVal( "gap" );
		select 	when $gap = -15 : 
				$gap := vl-SetAttributeVal( "gap", -5 );
			when $gap = -10 :
				$gap := vl-SetAttributeVal( "gap", -15 );
			when $gap = -5 :
				$gap := vl-SetAttributeVal( "gap", -10 );
		end;

		#************************
		# Place dimension macro
		#************************
		vl-PlaceMacro( "PCdimx", $csO, $csA, $formatX, 
					-$gap )
			[color = 13];
	end;

silent;







