;;***************************************************************************
;; RIS PC Utilities Installation - Intergraph Corporation
;;***************************************************************************

;;***************************************************************************
DefineVariables
;;***************************************************************************
;;09/13/93	rlk	0.0.0.5	Readme file from installationDirectory
;;				ctrl-X removed from visitOS
;;				added unqueall files if quesize = 0
;;				added risdcode to misc utilities
;;09/17/93	rlk	0.0.0.6	Vsrsion to be 04.03.03.07

Text [ScriptVersion] := 0.0.0.6
Text [CompanyName] := Intergraph Corporation
Text [ProductName]
Directory [ProductDirectory]
Directory [ReadmeFileName]
Directory [ReadmeInstructions]
Text [ProductVersion] := !version!
Directory [DefaultInstallationDirectory] := c:\RIS
Logical [OfferInstallationDirectoryChoice] := YesCharacter
Number [MinimumFilesSetting] := 0
Number [MinimumBuffersSetting] := 0
;;Directory [ProgramToRun] := BlankString
Logical [AddInstallDirectoryToPath] := NoCharacter
Text [MinimumOSVersion] := 3.0
Number [MinimumSystemMemoryRequiredPresent] := 0
Logical [AllowCGA] := YesCharacter
Logical [AllowMDA] := YesCharacter
Logical [Allow808x] := NoCharacter
Logical [Allow80286] := NoCharacter
Text [NetWorkInstallation] := AllowNetwork
Text [SpecialPopupColors] := WhiteOnBrown
Logical [NeedToTerminate] := NoCharacter
Logical [Installed] := NoCharacter
Text [DisplayAdapterSummary]
Text [CPUTypeSummary]
Text [NetworkActive]
Text [MathCoprocessorStatus]
Text [InstallationDriveSummary]
Text [OSVersionSummary]
Text [SystemMemorySummary]
Logical [InsufficientSystem] := NoCharacter
Text [BottomLine]
Directory [ResponseFIle]
EndDefineVariables

;; Set up Ris Values
[ProductName] := RIS Utilities
[ProductDirectory] := RISUTL

[ReadmeFileName] := [InstallationDirectory]\[ProductDirectory]\README.TXT
[ReadmeInstructions] := [BootDrive]:\[ProductDirectory].TXT

Do SetupInstalit
Do PrepareSystemReport

;; Find out if to run in auto mode
If [CommandLine1] Contains /f:
	[ResponseFile] := SubStringOf [CommandLine1] from 4 to End
	TextBox @xy(Center,4)
		Processing file [ResponseFile], please wait ...
	EndTextBox
	Do AutoInstallation
	SetExitMessageto "Update the autoexec.bat file before using [ProductName]"
	SoLong
Endif

:ShowSplashScreen
ClearScreen
;; Do Splash screen
TextBox @xy(Center,4)
	Welcome to the Intergraph Setup for the [ProductName] product.
	This setup will install the Utilities, either by a Basic Installation,
	Complete Installation, or by a Customized Installation (just the
	Utilities you want.)
EndTextBox
GetMenuChoice @xy(Center,12) UseHeader "[ProductName] - Version [ProductVersion]"
  Continue 
    ;Continues with the installation
  Exit Installation
    ;Exits the installation and returns to the DOS prompt
  Help
    ;Displays the product readme.txt file
EndGetMenuChoice

InCase choice is
	=2:	Do ExitInstallation
	=3:	Do ShowReadmeFile
			Goto ShowSplashScreen
EndInCase

ClearScreen
SetBottomLineTo "<F1> Help <F2> Exit to DOS <F3> System Summary <F4> Readme File <F9> to Quit"

;; get previous directory setting
[Dir1] := EnvironmentStringFor [ProductDirectory]
if [Dir1] <> BlankString
	[Number1] := PositionOf [ProductDirectory] in [Dir1]
	[Number1] := [Number1] -2
	[DefaultInstallationDirectory] := SubStringOf [Dir1] from beginning to [Number1]
Endif

If Windows  ;; INSTALIT for Windows does not currently support ScriptView
Popup This script was not designed for INSTALIT for Windows.
else  ;; by default ScriptView is off
	LetButton <F1> Do ShowHelpInfo
	LetButton <F2> Do VisitOS
	LetButton <F3> Do ShowSystemSummary
	LetButton <F4> Do ShowReadmeFile
	LetButton <F9> Do ExitInstallation
	LetButton <CtrlX> Do ExitInstallation
EndIf

;;***************************************************************************
:Again
;;***************************************************************************
ClearScreen

TextBox @xy(Center,4)
  Select the installation option you prefer.
EndTextBox
[Number5] := SizeOfGroup 1
[Number1] := [Number5] / 1024
[Number5] := SizeOfGroup 2
[Number2] := [Number5] / 1024
GetMenuChoice @xy(Center,10) UseHeader "Installation Options"
  Basic Installation
    ;Minimum features; Requires [Number1]K of disk space
  Complete Installation
    ;Complete features; Requires [Number2]K of disk space
  Customized Installation
    ;Allows for selection of specific features
	Exit
	;Exit the installation and return to the DOS prompt
EndGetMenuChoice

ClearScreen

InCase choice is
	=1:	QueFileGroup 1
	=2:	QueAllFiles
	=3:	Do CustomizedInstallation
	=4:	Do ExitInstallation
EndInCase

If QueSize = 0
	Goto Again
EndIf

If [InsufficientSystem] = YesCharacter
	Dialog [YesOrNo1] WhiteOnRed ;; Insufficient system notice
		Analysis of your system has determined that it is
		not compatible with [ProductName].  For a complete
		summary, press <F3>.

		Do you wish to continue anyway?
	EndDialog
	If [YesOrNo1] = NoCharacter
		GoTo Again
	EndIf
EndIf

If [Installed] = NoCharacter
	If [OfferInstallationDirectoryChoice] = YesCharacter
		DetermineInstallationDirectory [DefaultInstallationDirectory] QueSize RequireFixed
	else
		EndIf
EndIf

SetBottomLineTo     BlankString
Logging On
SetLogFileTo [InstallationDirectory]\[ProductDirectory].log

ClearScreen
GetQuedFiles ShowJustOne

SetBottomLineTo "<F1> Help <F2> Exit to DOS <F3> System Summary <F4> Readme File <F9> to Quit"

[Installed] := YesCharacter
If QueSize <> 0  ;; Some files were selected but not installed if <> 0
	[Installed] := NoCharacter
	UnqueAllFiles  ;; Remove uninstalled files from que
	Dialog ;; Message when unable to get all necessary files
		Unable to successfully complete this option.
	EndDialog
EndIf
:Continue
Do ExitInstallation


;;***************************************************************************
Procedure ShowHelpInfo
;;***************************************************************************
	Dialog PressAKey [SpecialPopupColors]  ;; How to reach technical support
		For assistance, call [CompanyName] Support.
		You are installing [ProductName] product.

		You can get a summary of your computer system
		by pressing <F3>.  This information will be
		important should you call Support.
	EndDialog
EndProcedure

;;***************************************************************************
Procedure ShowSystemSummary  ;; System summary - change with care.
;;***************************************************************************
	If [InstallationDirectory] <> BlankString
		[InstallationDriveSummary] := InstallationDrive (FreeSpaceOnDrive InstallationDrive bytes free)
	else
		[InstallationDriveSummary] := (Not Selected)
	EndIf
	Dialog PressAKey [SpecialPopupColors]
		.cHERE IS A SUMMARY OF YOUR SYSTEM
		.c[String8]  ;; notes whether any incompatibilities were found
		Product Name: [ProductName]
		Product Version: [ProductVersion]
		Installation Script Version: [ScriptVersion]

		Operating System Version:  [OSVersionSummary]
		CPU Type:  [CPUTypeSummary]
		Display Adapter:  [DisplayAdapterSummary]
		Math Coprocessor:  [MathCoprocessorStatus]
		Main Memory Present:  [SystemMemorySummary]
		Extended Memory Present:  ExtendedMemoryPresentK
		XMS Memory Available:  XMSMemoryAvailableK
		Installation Drive:  [InstallationDriveSummary]
		Network:  [NetworkActive]
		[String9]  ;; information about network suitability
	EndDialog
EndProcedure

;;***************************************************************************
Procedure PrepareSystemReport
;;***************************************************************************
	[InsufficientSystem] := NoCharacter
	If OSVersion < [MinimumOSVersion]
		[OSVersionSummary] := OSVersion (need at least [MinimumOSVersion])
		[InsufficientSystem] := YesCharacter
	else
		[OSVersionSummary] := OSVersion
	EndIf
	If MainMemory < [MinimumSystemMemoryRequiredPresent]
		[SystemMemorySummary] := MainMemoryK (need at least [MinimumSystemMemoryRequiredPresent]K)
		[InsufficientSystem] := YesCharacter
	else
		[SystemMemorySummary] := MainMemoryK
	EndIf
	If 808X
		If [Allow808X] = YesCharacter
			[CPUTypeSummary] := 808x
		else
			[CPUTypeSummary] := 808x (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If 80286
		[CPUTypeSummary] := 80286
	EndIf
	If 80386
		[CPUTypeSummary] := 80386
	EndIf
	If 80486
		[CPUTypeSummary] := 80486
	EndIf
	If [CPUTypeSummary] = BlankString
		[CPUTypeSummary] := 808x
	EndIf
	If MicroChannel
		[CPUTypeSummary] := [CPUTypeSummary] (in MicroChannel Architecture)
	else
		[CPUTypeSummary] := [CPUTypeSummary] (ISA or EISA Architecture)
	EndIf
	If CGA
		If [AllowCGA] = YesCharacter
			[DisplayAdapterSummary] := CGA
		else
			[DisplayAdapterSummary] := CGA (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If MDA
		If [AllowMDA] = YesCharacter
			[DisplayAdapterSummary] := MDA
		else
			[DisplayAdapterSummary] := MDA (not compatible with [ProductName])
			[InsufficientSystem] := YesCharacter
		EndIf
	EndIf
	If EGA
		[DisplayAdapterSummary] := EGA
	EndIf
	If VGA
		[DisplayAdapterSummary] := VGA
	EndIf
	If HGC
		[DisplayAdapterSummary] := HGC
	EndIf
	If HGCPlus
		[DisplayAdapterSummary] := HGC Plus
	EndIf
	If Network
		[NetworkActive] := Network Active
	else
		[NetworkActive] := No Network Active
	EndIf
	If NovellLoaded
		[NetworkActive] := Novell [NetworkActive]
	EndIf
	If MathCoprocessorPresent
		[MathCoprocessorStatus] := Present
	else
			[MathCoprocessorStatus] := Not Present
	EndIf

	If [InsufficientSystem] = YesCharacter
		[String8] := (Please note the listed incompatibilities)
	EndIf
	If [NetworkInstallation] contains DisallowNetwork
		[String9] := "   (You cannot install this version on a network drive)"
	EndIf
EndProcedure

Procedure VisitOS
	SetAllowExit Off
	Dialog [YesOrNo1] [SpecialPopupColors]  ;; Notice before exiting to OS
		Do you wish to exit to the operating system and
		then return?
	EndDialog
	If [YesOrNo1] = YesCharacter
		ExitToDOS
	EndIf
	SetAllowExit On
EndProcedure

;;***************************************************************************
Procedure ExitInstallation
;;***************************************************************************
	SetAllowExit Off

	ClearScreen

	;; Ask to read the product readme.txt file
	If [Installed] = YesCharacter
		Do ShowReadmeFile
	EndIf
	If [Installed] = YesCharacter
		Dialog ;; Final Farewell
			Installation for [ProductName] is complete!
		EndDialog
		SetExitMessageto "Update the autoexec.bat file before using [ProductName]"
	Endif
	SoLong
EndProcedure

;;***************************************************************************
Procedure SetupInstalit
;;***************************************************************************
	SetShadows On
  SetBackgroundCharTo B0
  SetMainBorderTo     None
  SetPopupBorderTo    Single
  SetModifyModeTo     Example
  SetShadowAttrTo     DarkGrayOnBlack
  SetScreenAttrTo     WhiteOnBlue
  SetPopupAttrTo      BlueOnWhite
  SetAttentionAttrTo  WhiteOnRed
  SetTopLineAttrTo    BlackOnCyan
  SetBottomLineAttrTo WhiteOnLightGray

  SetTopLineTo        "  [CompanyName]'s [ProductName] Version [ProductVersion]  "

EndProcedure

;;***************************************************************************
Procedure ShowReadmeFile
;;***************************************************************************
;; Ask to read the product readme.txt file
If [Installed] = YesCharacter
	[ReadmeFileName] := [InstallationDirectory]\[ProductDirectory]\readme.txt
else
	[ReadmeFileName] := [InstallFromDirectory]readme.txt
Endif

If [Installed] = YesCharacter
	Dialog [YesOrNo1]
		Do you want to read the [ProductName] readme file now?
	EndDialog
else
	[YesOrNo1] := YesCharacter
Endif
	If [YesOrNo1] = YesCharacter
		If FileExists [ReadmeFileName]
			BrowseFile [ReadmeFileName]
		else
			Popup Sorry, readme file [ReadMeFileName] is missing.
		EndIf
	EndIf
EndProcedure


Procedure CustomizedInstallation
	TextBox @xy(Center,3)
		Which installation options do you want?
		Use <SPACEBAR> to toggle option, F10 to toggle ALL options
		Use <RETURN> for next menu, <ESC> for previous menu
	EndTextBox
	[Number5] := SizeOfGroup 1
	[Number1] := [Number5] / 1024
	[Number5] := SizeOfGroup 11
	[Number2] := [Number5] / 1024
	[Number5] := SizeOfGroup 12
	[Number3] := [Number5] / 1024
	[Number5] := SizeOfGroup 13
	[Number4] := [Number5] / 1024
	SetAllowEscape On
	GetMenuChoice @xy(Center,10) UseHeader "Customized Installation" PreSelectAll AllowIfNone
		Basic Installation
			;Minimum features; Requires [Number1]K of disk space
		Load/Unload utilities
			;Contains the load and unload utilities; Requires [Number2]K of disk space
		Playback/Record Utilities
			;Contains the playback and record utilities; Requires [Number3]K of disk space
		Miscellaneous Utilities
			;Contains the data type, declare, and dcode utilities;
			;Requires [Number4]K of disk space
	EndGetMenuChoice
	InCase choice is
		=1:	QueFileGroup 1
		=2:	QueFileGroup 11
		=3:	QueFileGroup 12
		=4:	QueFileGroup 13
	EndInCase
	SetAllowEscape Off
	If Escaped
		UnQueAllFiles
		Goto Again
	Endif
EndProcedure

Procedure AutoInstallation
	[Number2] := 999
	ExamineTextFile [ResponseFile]
		[DefaultInstallationDirectory] := SettingFor DestPath
		[String1] := SettingFor InstallType
	ForgetTextFile
	If [String1] = Basic
		[Number2] := 1
		QueFileGroup 1
	Endif
	If [String1] = Complete
		[Number2] := 2
		QueAllFiles
	Endif
	if [Number2] = 999
		Popup "Invalid InstallType
		SoLong
	Endif

	[InstallationDirectory] := [DefaultInstallationDirectory]
	ClearScreen
	Logging On
	SetLogFileTo [InstallationDirectory]\[ProductDirectory].log

	GetQuedFiles ShowJustOne
	[Installed] := YesCharacter
EndProcedure



