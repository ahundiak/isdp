#******************************************************************************
#	STRUDEL FILE FOR AVONDALE PROFILE CARDS
#
#	Maciej Jablkowski - INTERGRAPH Corp., March 9th, 2000
#
# Strudel file contains rules for extracting 3D model stiffeners 
# to the 2D profile card not-to-scale manufacturing sketches
#
# This file contains common code for penetrations (Cut Throughs)
#
#	This file uses external PPL program "PcGen.e" which helps to 
#	determine a name of a 2D symbol macro to place on the profile
#	card from striffener type being processed and aplied macros.
#
# History:	MJ	03-09-2000 - Creation
#		MJ	08-02-2001 - Major review and update.
#		MJ	09-18-2001 - PPL calls reduced by using attributes
#
#******************************************************************************

# Turn report mode on/off

silent;
#verbose;


#*********************************************************
#
#	STF OPENINGS
#
#*********************************************************

const(	definition	= "generic_cutter*",
	view-criterion	= vl-pc-McDefAttribute("~~operator0~0") ) : {

	top, bottom : {

		#*****************************
		# Get view & macro coord sys
		#*****************************
		$csO 		:= vl-pc-ViewCs();
		$csA 		:= vl-pc-ViewMcCs();

                $profile_length := vl-GetAttributeVal( "ProfileLength" );
		$start_stock    := vl-GetAttributeVal( "StartStock" );
                $FirstEtPos     := vl-GetAttributeVal( "FirstETPos" );
                $macPos         := vl-pc-McPosition();
		$macPosX	:= vl-pc-CvrtDouble( get-word( $macPos, 0 ));
                        
		#**************************************************************
		# Proceed only if macro is placed between end treatments
		#**************************************************************
                select when $macPosX > $FirstEtPos + 15
                	& $macPosX < $profile_length -15 :
                        
			#****************************************
			# Put a X dimension on the profile card
			#****************************************
	               	$xbase          := vl-pc-GetXBase( 0 );
			$positionX 	:= $macPosX-$FirstEtPos+$xbase+$start_stock;
			$numcharX 	:= string-length($positionX); 
			$formatX 	:= format-number(
						catenate-strings($numcharX,
							".0") , $positionX );

			#***********************
			# Set dimension offset
			#***********************
			$gap 		:= vl-GetAttributeVal( "gap" );

			select 	when $gap = -15 : 
					$gap := vl-SetAttributeVal( "gap", -5 );
				when $gap = -10 :
					$gap := vl-SetAttributeVal( "gap", -15 );
				when $gap = -5 :
					$gap := vl-SetAttributeVal( "gap", -10 );
			end;

			#***********************
			# Place dimension macro
			#***********************
			vl-PlaceMacro( "PCdimx", $csO, $csA, $formatX, 
					-$gap )[color = 13];
		end;
	}
}

silent;